apply plugin: io.pry.gradle.offline_dependencies.OfflineDependenciesPlugin

evaluationDependsOn(':gradleriobase')

def otherDeps = []


configurations {
  offline
  tools
}

def toolArtifacts = [
  'edu.wpi.first.shuffleboard:app',
  'edu.wpi.first.wpilib:OutlineViewer',
  'edu.wpi.first.wpilib:SmartDashboard',
  'edu.wpi.first.wpilib:RobotBuilder'
]

def lazyEvaluator = tasks.register('lazyModelEvaluation') {

  dependsOn project(':gradleriobase').tasks.named('modelEvaluationTrigger')
  doLast {
    def gradleRioDeps = project(':gradleriobase').getGradleRioDependencies()
    dependencies {
      gradleRioDeps.deps.each { dep->
        if (dep.classifier == null) {
          offline "$dep.groupId:$dep.artifactId:$dep.version:@$dep.extension"
        } else if (dep.classifier == 'windowsx86-64') {
          offline "$dep.groupId:$dep.artifactId:$dep.version:windowsx86@$dep.extension"
          offline "$dep.groupId:$dep.artifactId:$dep.version:$dep.classifier@$dep.extension"
        } else if (dep.classifier == 'windowsx86') {
          offline "$dep.groupId:$dep.artifactId:$dep.version:windowsx86-64@$dep.extension"
          offline "$dep.groupId:$dep.artifactId:$dep.version:$dep.classifier@$dep.extension"
        } else {
          offline "$dep.groupId:$dep.artifactId:$dep.version:$dep.classifier@$dep.extension"
        }

        toolArtifacts.each { tool->
          if ("$dep.groupId:$dep.artifactId" == tool && (dep.classifier == null || dep.classifier == "")) {
            if (dep.classifier == null) {
              tools "$dep.groupId:$dep.artifactId:$dep.version:@$dep.extension"
            } else {
              tools "$dep.groupId:$dep.artifactId:$dep.version:$dep.classifier@$dep.extension"
            }
          }
        }


      }
    }
  }
}
repositories {
    project(':gradleriobase').getGradleRioUrls().each { gUrl->
        maven {
          url = gUrl
        }
    }
}

updateOfflineRepository.dependsOn lazyEvaluator

offlineDependencies {
  repositories {
    // You'll have to add your buildscript repositories here too
    project(':gradleriobase').getGradleRioUrls().each { gUrl->
      maven {
        url = gUrl
      }
    }
  }

  configurations 'offline'

  includeSources = true
  includeJavadocs = true
  includePoms = true
  includeIvyXmls = true
}

ext.mavenConfigSetup = {
  return new Tuple2({ task->
  }, { config->
    config['Maven'] = [:]
    config['Maven']['Folder'] = 'maven'
    config['Maven']['MetaDataFixerExe'] = 'MavenMetaDataFixer.exe'
  })
}

ext.mavenZipSetup = { Zip zip->
  zip.dependsOn updateOfflineRepository
  zip.inputs.dir offlineRepositoryRoot
  zip.from(fileTree(offlineRepositoryRoot)) {
    into '/maven'
  }
}
