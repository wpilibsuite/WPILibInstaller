import org.gradle.internal.os.OperatingSystem

apply plugin: 'com.ullink.msbuild'
apply plugin: 'com.ullink.nuget'

def slnFile = 'DotNet/WPILibInstaller.sln'

nugetSources {
  operation 'list'
}

nugetRestore {
  solutionFile slnFile
}

msbuild {
  dependsOn nugetRestore

  solutionFile slnFile

  targets = ['Clean', 'Rebuild']

  if (project.hasProperty('linuxBuild')) {
    parameters.platform = 'Linux'
  } else if (project.hasProperty('macBuild')) {
    parameters.platform = 'Mac'
  }

  parameters.configuration = 'Release'
}

ext.installerFile = file('DotNet/WPILibInstaller/bin/Release/Merged/WPILibInstaller.exe')
def mavenUpdater = file('DotNet/MavenMetaDataFixer/bin/Release/MavenMetaDataFixer.exe')
def toolsUpdater = file('DotNet/ToolsUpdater/bin/Release/Merged/ToolsUpdater.exe')

ext.msBuildSetupMain = { AbstractArchiveTask zip->
  zip.dependsOn msbuild
  zip.inputs.file installerFile

  zip.from (installerFile)
}

ext.msBuildExtraSetup = { AbstractArchiveTask zip->
  zip.dependsOn msbuild
  zip.inputs.file mavenUpdater
  zip.inputs.file toolsUpdater

  zip.from (mavenUpdater) {
    into '/maven'
  }

  zip.from (toolsUpdater) {
    into '/tools'
  }
}
