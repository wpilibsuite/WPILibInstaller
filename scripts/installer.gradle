import org.gradle.internal.os.OperatingSystem

apply plugin: 'com.ullink.msbuild'
apply plugin: 'com.ullink.nuget'

def slnFile = 'DotNet/WPILibInstaller.sln'

evaluationDependsOn(':apps:MavenMetaDataFixer')

nugetSources {
  operation 'list'
}

nugetRestore {
  solutionFile slnFile
}

msbuild {
  dependsOn nugetRestore

  solutionFile slnFile

  targets = ['Clean', 'Rebuild']

  if (project.hasProperty('linuxBuild')) {
    parameters.platform = 'Linux'
  } else if (project.hasProperty('macBuild')) {
    parameters.platform = 'Mac'
  }

  parameters.configuration = 'Release'
}

ext.installerFile = file('DotNet/WPILibInstaller/bin/Release/Merged/WPILibInstaller.exe')
def toolsUpdater = file('DotNet/ToolsUpdater/bin/Release/Merged/ToolsUpdater.exe')

ext.msBuildSetupMain = { AbstractArchiveTask zip->
  zip.dependsOn msbuild
  zip.inputs.file installerFile

  zip.from (installerFile)
}

def metaDataScriptBatch = file("$rootDir/files/MavenMetaDataFixer.bat")
def metaDataScriptSh = file("$rootDir/files/MavenMetaDataFixer.sh")

ext.msBuildExtraSetup = { AbstractArchiveTask zip->
  zip.dependsOn msbuild
  zip.inputs.file project(':apps:MavenMetaDataFixer').shadowJar.archivePath
  zip.inputs.file toolsUpdater
  zip.inputs.file metaDataScriptBatch
  zip.inputs.file metaDataScriptSh

  zip.from (project(':apps:MavenMetaDataFixer').shadowJar.archivePath) {
    into '/maven'
  }

  if (project.ext.isUnix == true) {
    zip.from(metaDataScriptSh) {
      into '/maven'
      fileMode 0755
    }
  } else {
    zip.from(metaDataScriptBatch) {
      into '/maven'
    }
  }

  zip.from (toolsUpdater) {
    into '/tools'
  }
}
